buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
  }
}

apply plugin: 'org.asciidoctor.convert'

repositories {
  jcenter()
}

configurations.asciidoctor {
  resolutionStrategy {
    force 'org.asciidoctor:asciidoctorj-groovy-dsl:1.0.0.Alpha2'
  }
}

asciidoctor {
  asciidoctorj {
    version = '1.5.4.1'
  }
  backends 'html5'
  sourceDir file('docs')
  sources {
    include '*.adoc'
  }
  resources {
    from('resources') {
      include '*'
    }
  }
  outputDir file('build')
  options doctype: 'book'
  extensions {
    docinfo_processor { document ->
      def attrs = document.attributes.findAll { key, value ->
        String val = value.toString()
        val.startsWith('{{') && val.endsWith('}}')
      }.keySet()

       """<script>
       var attributes = [${attrs.collect { "'${it}'" }.join(', ')}];
</script>"""
    }
  }
}

defaultTasks 'asciidoctor'
